[opheld1,bullroarer]
if(add(%action_delay, 8) > map_clock) {
    mes("You're a bit too busy to do that at the moment.");
    return;
}
// https://www.youtube.com/live/8fW1_vt07Z4?si=dcIwsma5escAVDrQ&t=2010, nothing found, not in jungle zones
// has a 1t delay between first and second mes, unlike osrs (delay between messages is slightly different in general)
%action_delay = map_clock;
mes("You start to swing the bullroarer above your head.");
anim(human_bullroarer, 0);
sound_synth(bullroarer, 0, 0);
spotanim_pl(bullroarer, 92, 0);
queue(bullroarer_use, 1);

[queue,bullroarer_use]
mes("You feel a bit silly at first, but soon it makes an interesting sound.");
if (~inzone_coord_pair_table(kharazi_jungle, coord) = true) {
    npc_findallany(coord, 10, 0);
    while (npc_findnext = true) {
        // seems to be a random chance, they won't always attack you
        if((npc_type = oomlie_bird | npc_type = jungle_savage | npc_type = jungle_wolf) & random(6) = 0) {
            mes("A nearby <npc_name> takes a dislike to the noise you're making.");
            ~npc_retaliate(0);
            return;
        }
    }
    if(random(6) < 5) {
        mes("You see some movement in the trees...");
        p_delay(0);
        def_coord $coord = map_findsquare(coord, 1, 7, ^map_findsquare_lineofwalk);
        mes("...and a tall, dark, charismatic looking native approaches you.");
        // You can spawn multiple Gujuos by running far away enough
        if(npc_find(coord, gujuo, 14, 0) = false) {
            npc_add($coord, gujuo, 500);
        }
        if (lineofwalk(npc_coord, coord) = false) {
            // https://www.youtube.com/live/8fW1_vt07Z4?si=4bYW5uI0t6fEvN7q&t=4505
            mes("It looks like the native cannot get to you from here.");
            return;
        }
        npc_walk(movecoord(coord, 1, 0, 0));
        npc_setmode(opplayer2);
        return;
    }
    mes("But nothing much seems to happen.");
    return;
}
// this is a 5t weakqueue in osrs, and from what I can tell, is also queued on release osrs/pre-eoc
// could've been changed 2005-2007? (pre-eoc example weakqueued: https://youtu.be/oHcazhvs5-U?si=vlP-co5HhCgkS8GT&t=55)
queue(bullroarer_out_jungle, 4);

[queue,bullroarer_out_jungle]
mes("Nothing much seems to happen though.");
npc_findallany(coord, 4, 0);
while (npc_findnext = true) {
    if(npc_type = jungleforester_m | npc_type = jungleforester_f) {
        ~chatnpc("<p,happy>You might like to use that when you get into the Kharazi jungle, it might attract more natives...");
        return;
    }
}
