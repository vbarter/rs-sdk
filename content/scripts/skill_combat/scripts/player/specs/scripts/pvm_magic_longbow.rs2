[label,pvm_magic_longbow_sa]
def_obj $rhand = inv_getobj(worn, ^wearpos_rhand);
def_obj $ammo = ~player_ranged_check_ammo($rhand); 
if ($ammo = null) {
    p_stopaction;
    ~update_all($rhand);
    return;
}

~set_sa_vars(oc_param($rhand, sa_energy));

anim(%com_attackanim, 0);
spotanim_pl(sp_attack_glow_arrow_launch, 96, 0);
sound_synth(powershot, 0, 0);
def_int $duration = ~npc_projectile(coord, npc_uid, sp_attack_glow_arrow_travel, 40, 36, 41, 15, 5, 11, 5);
~ranged_dropammo_npc($ammo, sub(divide($duration, 30), 1));

def_int $combat_stat = ~combat_stat(add(stat(ranged), 10), oc_param($ammo, rangebonus)); // + 10 ranged levels
def_int $maxhit = ~combat_maxhit($combat_stat);
if (npc_type = count_draynor & inv_total(inv, garlic) > 0) {
    $maxhit = add($maxhit, 1);
}
def_int $damage = randominc(min($maxhit, npc_param(max_dealt)));
def_int $damage_capped = min($damage, npc_stat(hitpoints));
~give_combat_experience(%damagestyle, $damage_capped, %npc_combat_xp_multiplier);
npc_heropoints($damage_capped);

def_int $poison_severity = oc_param($ammo, poison_severity);
if ($damage > 0 & $poison_severity > 0 & random(4) = 0) { // 1/4 chance to poison
    // poison npc
    ~npc_poison_start($poison_severity);
}
~npc_retaliate(add(divide($duration, 30), 1));
npc_queue(2, $damage, add(divide($duration, 30), 1));
// No npc anim in osrs?
if (npc_param(defend_sound) ! null) {
    sound_synth(npc_param(defend_sound), 0, $duration); // delay 1 client tick for the hit queue
}